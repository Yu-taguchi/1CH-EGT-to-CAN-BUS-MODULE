/****************************************
2025/03/15 Rev 2.0
ボートVer2.0に対応
ダイアグ通知LED通知、およびID・通信速度切替機能の追加
****************************************/

#include "Adafruit_MAX31855.h"
#include "mcp2515.h"
#define MAXCS 9
Adafruit_MAX31855 thermocouple(MAXCS);
struct can_frame canMsg1;
MCP2515 mcp2515(10);
unsigned int a, bH, bL;
unsigned int counter=0;

void setup() {
  pinMode(1,INPUT_PULLUP);//UNIT ID
  pinMode(2,INPUT_PULLUP);//BUS_SPEED1
  pinMode(3,INPUT_PULLUP);//BUS_SPEED2
  pinMode(4, OUTPUT); //PD4 DIAG LED

  if(digitalRead(1) == HIGH)
  {
    canMsg1.can_id = 0x5A0;//送信IDを変更する場合は、ココの値を変更する。(例)LINK G4XのIDが1440(10進数)の場合、0x5A0(16進数)を記入。数字冒頭の0xは16進数の値である事を示すおまじない。
  }else if (digitalRead(1)==LOW){
    canMsg1.can_id = 0x5A1;
  }
  canMsg1.can_dlc = 5;
  mcp2515.reset();
  if(digitalRead(2) == LOW){
    mcp2515.setBitrate(CAN_125KBPS, MCP_8MHZ);
  }else if(digitalRead(3)==LOW){
    mcp2515.setBitrate(CAN_1000KBPS, MCP_8MHZ);
  }else{
    mcp2515.setBitrate(CAN_500KBPS, MCP_8MHZ);
  }
  mcp2515.setNormalMode();
  delay(500);
}

void loop() {

  //測温接点温度(T1)の取得
  double c = thermocouple.readCelsius();
  //基準接点温度(T0)の取得
  double d = thermocouple.readInternal();
  //故障診断
  if (isnan(c)) {
    canMsg1.data[4] = 0x03; //FAIL
    counter=counter+1;
    if(counter<25)
    {
      digitalWrite(4, LOW);
    }else{
      digitalWrite(4, HIGH);
    }
    if( counter>50){
      counter=0;
    }

  } else {
    canMsg1.data[4] = 0x01; //Active
    digitalWrite(4, LOW);
    counter=0;
  }

  //取得温度(T0)のデータセット
  c = c * 10;
  int k = (int)c;
  bH = k >> 8;
  bL = k & 0x00FF;
  canMsg1.data[0] = bH;  //送信内容
  canMsg1.data[1] = bL;  //送信内容
  //取得温度(T1)のデータセット
  d = d * 10;
  k = (int)d;
  bH = k >> 8;
  bL = k & 0x00FF;
  canMsg1.data[2] = bH;  //送信内容
  canMsg1.data[3] = bL;  //送信内容

  //データ送信
  mcp2515.sendMessage(&canMsg1);
  delay(20);
}
